plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'edu.sc.seis.launch4j' version '2.5.3'
    id 'org.beryx.runtime' version '1.12.7'
}

group = 'it.centoreluca'
version = '1.0.0'

repositories {
    mavenCentral()
}

application {
    mainClass.set("it.centoreluca.Main")
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    /* DATABASE*/
    implementation 'org.postgresql:postgresql:42.3.6'
    /* JSON */
    implementation 'org.json:json:20220320'
    /* ESPORTAZIONE EXCEL */
    implementation 'org.apache.poi:poi:5.2.0'
    implementation 'org.apache.poi:poi-ooxml:5.2.0'
    shadow 'org.apache.poi:poi:5.2.0'
    shadow 'org.apache.poi:poi-ooxml:5.2.0'
    /* LOGGING */
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    shadow 'org.apache.logging.log4j:log4j-core:2.17.2'
    /* ICONE */
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'
}

launch4j {
    headerType = 'console'
    companyName = 'centore luca'
    copyright = 'centore luca'
    version = '1.0.0.1'
    icon = "${projectDir}/src/main/resources/it/centoreluca/icon/hairdresser.ico"
    jarTask = project.tasks.shadowJar
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions = ['--win-console']
        } else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'non-modular-hello','--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'non-modular-hello']
            installerType = 'dmg'
        }
    }
}